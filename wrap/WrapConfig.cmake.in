# -*- cmake -*-

# Copyright (c) 2008, Lawrence Livermore National Security, LLC. 
# Written by Martin Schulz, schulzm@llnl.gov, LLNL-CODE-402774,
# All rights reserved - please read information in "LICENCSE"

# TODO Martin please add the copyright statment of your choice, as well as 
#      a reference to the license or license file!
#
# This files is partially from the MUST project and bears the follwing copyright.
# Copyright (c) 2009, ZIH, Technische Universitaet Dresden, Federal Republic of Germany
# Copyright (c) 2009, LLNL, United States of America
#
# @file WrapConfig.cmake
#       Contains macros for using the wrap script in a build environment.
#
# @author Todd Gamblin
# @date 19 May 2011

if (NOT Wrap_CONFIG_LOADED)
  set(Wrap_CONFIG_LOADED TRUE)

  # This variable allows users to use the wrap.py script directly, if desired.
  set(Wrap_EXECUTABLE @WRAP@)

  # add_wrapped_file(file_name wrapper_name [flags])
  # 	
  # This macro adds a command to generate <file_name> from <wrapper_name> to the
  # build.  Properties on <file_name> are also set so that CMake knows that it
  # is generated.
  # 
  # Optionally, flags may be supplied to pass to the wrapper generator.
  #
  function(add_wrapped_file file_name wrapper_name)
    set(file_path    ${CMAKE_CURRENT_BINARY_DIR}/${file_name})
    set(wrapper_path ${CMAKE_CURRENT_SOURCE_DIR}/${wrapper_name})

    # Play nice with FindPythonInterp -- use the interpreter if it was found,
    # otherwise use the script directly.
    if (PYTHON_EXECUTABLE)
      set(command ${PYTHON_EXECUTABLE})
      set(script_arg ${Wrap_EXECUTABLE})
    else()
      set(command ${Wrap_EXECUTABLE})
      set(script_arg "")
    endif()

    if (ARGN)
      # Prefer directly passed in flags.
      list(GET ARGN 0 wrap_flags)
    else()
      # Otherwise, look in the source file properties
      get_source_file_property(wrap_flags ${wrapper_name} WRAP_FLAGS)
      if (wrap_flags STREQUAL NOTFOUND)
        # If no spefific flags, grab them from the WRAP_FLAGS environment variable.
        set(wrap_flags "")
        if (NOT WRAP_FLAGS STREQUAL "")
          set(wrap_flags "${WRAP_FLAGS}")
        endif()
      endif()
    endif()

    SET (compiler_to_use "${CMAKE_C_COMPILER}")
    IF (MPI_C_COMPILER)
    	    SET (compiler_to_use "${MPI_C_COMPILER}")
    ENDIF ()

    # Add a command to automatically wrap files.
    add_custom_command(
      OUTPUT  ${file_path}
      
      COMMAND ${command} ARGS ${script_arg} ${wrapper_path} -c ${compiler_to_use} ${wrap_flags} -o ${file_path}
      WORKING_DIRECTORY "${CMAKE_CURRENT_BINARY_DIR}"
      DEPENDS ${wrapper_path}
      COMMENT "Generating ${file_name} from ${wrapper_name}"
      VERBATIM)

    # Mark target file as generated so the build system knows what to do w/it
    set_source_files_properties(${file_path} PROPERTIES GENERATED TRUE)

    # Add generated files to list of things to be cleaned for the directory.
    get_directory_property(cleanfiles ADDITIONAL_MAKE_CLEAN_FILES)
    list(APPEND cleanfiles ${file_name})
    set_directory_properties(PROPERTIES ADDITIONAL_MAKE_CLEAN_FILES "${cleanfiles}")
  endfunction()

endif()
